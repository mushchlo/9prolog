/*  File   : pl/grammar
    Author : Fernando Pereira
    Updated: Wednesday February 1st, 1984, 8:38:15 pm
    Purpose: translation of grammar rules
*/


$translate_rule((LP-->[]), H) :- !,
	$t_head(LP, S, S, H).
$translate_rule((LP-->RP), (H:-B)):-
	$t_head(LP, S, SR, H),
	$t_body(RP, S, SR, B1),
	$t_tidy(B1, B).


$t_head((LP,List), S, SR, H):- !,
	$append(List, SR, List2),
	$extend([S,List2], LP, H).
$t_head(LP, S, SR, H) :-
	$extend([S,SR], LP, H).


$t_body(!, S, S, !) :- !.
$t_body([], S, S1, S=S1) :- !.
$t_body([X], S, SR, 'C'(S,X,SR)) :- !.
$t_body([X|R], S, SR, ('C'(S,X,SR1),RB)) :- !,
	$t_body(R, SR1, SR, RB).
$t_body({T}, S, S, T) :- !.
$t_body((T,R), S, SR, (Tt,Rt)) :- !,
	$t_body(T, S, SR1, Tt),
	$t_body(R, SR1, SR, Rt).
$t_body((T;R), S, SR, (Tt;Rt)) :- !,
	$t_body(T, S, S1, T1), $t_fill(S, SR, S1, T1, Tt),
	$t_body(R, S, S2, R1), $t_fill(S, SR, S2, R1, Rt).
$t_body(T, S, SR, Tt) :-
	$extend([S,SR], T, Tt).


$t_fill(S, SR, S1, T, (T,SR=S)) :-
	S1 == S, !.
$t_fill(S, SR, SR, T, T).


$extend(More, OldT, NewT) :-
	OldT =.. OldL,
	$append(OldL, More, NewL),
	NewT =.. NewL.


$append([], L, L) :- !.
$append([H|T], L, [H|R]) :-
	$append(T, L, R).


$t_tidy((P1;P2), (Q1;Q2)) :- !,
	$t_tidy(P1, Q1),
	$t_tidy(P2, Q2).
$t_tidy(((P1,P2),P3), Q) :-
	$t_tidy((P1,(P2,P3)), Q).
$t_tidy((P1,P2), (Q1,Q2)) :- !,
	$t_tidy(P1, Q1),
	$t_tidy(P2, Q2).
$t_tidy(A, A).


'C'([X|S],X,S).


phrase(PhraseDef, WordList) :-
	$t_body(PhraseDef, WordList, [], Goal), !,
	call(Goal).


